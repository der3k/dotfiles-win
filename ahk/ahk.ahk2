#Requires AutoHotkey v2.0
InstallKeybdHook()
Persistent()
A_HotkeyInterval := 100

SetKeyDelay(-1)

TraySetIcon("ahk.ico")

home := EnvGet("USER_HOME")
desktop := EnvGet("USER_DESKTOP")
console := EnvGet("USER_CONSOLE")
editor := EnvGet("USER_EDITOR")
viewer := EnvGet("USER_VIEWER")

SetWorkingDir(desktop)

:O*:e,,::rsi.news@quick.cz
:O*:eg,,::richard.sikora@gmail.com
:O*:et,,::richard.sikora@tietoevry.com
:O*:t,,::739524873
:O*:s,,::Slezská 1851/40
:TO*:n,,::Nádražní 42/4
:O*:c,,::Český Těšín
:O*:p,,::73701
:O*:o,,::012846595

+<^h:: { ; PAD LEFT
  WinGetPos(&x, &y, &w, &h, "A")
  relWidth := Round((w / A_ScreenWidth) * 100, 2)
  if (x <= 0) {
    ; active window is left padded
    if (relWidth <= 25) {
      ; on 25% or less => PAD LEFT 33.33%
      WinMove(-4, y, A_ScreenWidth / 3, h, "A")
    } else if (relWidth <= 33.33) {
      ; on 33.33% or less => PAD LEFT 50%
      WinMove(-4, y, A_ScreenWidth / 2, h, "A")
    } else if (relWidth <= 50) {
      ; on 50% or less => PAD LEFT 66.66%
      WinMove(-4, y, A_ScreenWidth * 0.66, h, "A")
    } else {
      ; ? => 25% left padded => PAD LEFT 25%
      WinMove(-4, y, A_ScreenWidth / 4, h, "A")
    }
  } else {
    ; PAD LEFT 33.33%
    WinMove(-4, 0, A_ScreenWidth / 3, A_ScreenHeight + 7, "A")
  }
}

+<^l:: { ; PAD RIGHT
  WinGetPos(&x, &y, &w, &h, "A")
  relWidth := Round((w / A_ScreenWidth) * 100, 2)
  if (x + w >= (A_ScreenWidth - 5)) {
    ; active window is right padded
    if (relWidth <= 25) {
      ; on 25% or less => PAD RIGHT 33.33%
      WinMove(A_ScreenWidth - A_ScreenWidth / 3, y, A_ScreenWidth / 3, h, "A")
    } else if (relWidth <= 33.33) {
      ; on 33.33% or less => PAD RIGHT 50%
      WinMove(A_ScreenWidth - A_ScreenWidth / 2, y, A_ScreenWidth / 2, h, "A")
    } else if (relWidth <= 50) {
      ; on 50% or less => PAD RIGHT 66.66%
      WinMove(A_ScreenWidth - A_ScreenWidth * 0.66, y, A_ScreenWidth * 0.66, h, "A")
    } else {
      ; ? => PAD RIGHT 25%
      WinMove(A_ScreenWidth - A_ScreenWidth / 4, y, A_ScreenWidth / 4, h, "A")
    }
  } else {
    ; ? => PAD RIGHT 33.33%
    WinMove(A_ScreenWidth - A_ScreenWidth / 3, 0, A_ScreenWidth / 3 + 14, A_ScreenHeight + 7, "A")
  }
}

+<^i:: { ; CENTER
  maximized := WinGetMinMax("A")
  if (maximized = 1) {
    WinRestore("A")
    return
  }
  WinGetPos(&x, &y, &w, &h, "A")
  relWidth := Round((w / A_ScreenWidth) * 100, 2)
  if (relWidth > 33.33 || (x <= 0 && relWidth > 25) || (x + w >= (A_ScreenWidth - 5) && relWidth > 25)) {
    WinMove(A_ScreenWidth / 3 - 18, 0, A_ScreenWidth / 3 + 36, A_ScreenHeight + 7, "A")
  } else {
    WinMove(A_ScreenWidth / 4 - 18, 0, A_ScreenWidth / 2 + 36, A_ScreenHeight + 7, "A")
  }
}

+<^k:: { ; UP
  WinGetPos(&x, &y, &w, &h, "A")
  if (h < A_ScreenHeight) {
    ; ? + up => full height
    WinMove(x, 0, w, A_ScreenHeight, "A")
  } else {
    ; full height + up => top half height
    WinMove(x, 0, w, A_ScreenHeight / 2, "A")
  }
}

+<^j:: { ; DOWN
  WinGetPos(&x, &y, &w, &h, "A")
  if (h < A_ScreenHeight) {
    ; ? + down => full height
    WinMove(x, 0, w, A_ScreenHeight, "A")
  } else {
    ; full height + down => bottom half height
    WinMove(x, A_ScreenHeight / 2, w, A_ScreenHeight / 2, "A")
  }
}

*<!n::WinMinimize("A")

*<!m:: {
  if WinActive("ahk_exe CDViewer.exe") {
    SendInput("^q")
  } else {
    Send("{Alt down}{Tab}")
  }
}

+<^r::Reload()

#a:: {
  A_Clipboard := A_Clipboard  ; strip formatting
}

#^t:: {
  timeStamp := FormatTime(, "yyyy-MM-dd hh:mm:ss")
  SendInput(timeStamp)
}

#HotIf WinActive("ahk_exe alacritty.exe")
;  <^k::Send("{Blind}!k")
#HotIf

; Directory window & Explorer
GroupAdd("ExplorerGroup", "ahk_class CabinetWClass")
GroupAdd("ExplorerGroup", "ahk_class ExploreWClass")

#HotIf WinActive("ahk_group ExplorerGroup")
F3::openSelectionInViewer()
;F4::openSelection()
;+F4::createFileFromWinTitle()
F7::createDirFromWinTitle()
^d::openDosFromWinTitle()
#HotIf

openDosFromWinTitle() {
  global console
  dir := WinGetTitle("A")
  Run(console ' -d "' dir '"')
}

openSelection() {
  global editor
  selection := getSelection()
  try
  attribs := FileGetAttrib(selection)
  catch
    return
  if InStr(attribs, "D")
    openDir(selection)
  else
    Run(editor ' "' selection '"')
}

openDir(path) {
  Run("explorer /n," path)
}

getDirFromWinTitle() {
  return WinGetTitle("A")
}

createDirFromWinTitle() {
  dir := WinGetTitle("A")
  createDirOnPath(dir)
}

createDirOnPath(dir) {
  result := InputBox("", "New folder name", "w300 h100")
  if (result.Result != "OK")
    return
  path := dir "\" result.Value "\"
  DirCreate(path)
  openDir(path)
}

createFileFromWinTitle() {
  dir := WinGetTitle("A")
  createFileOnPath(dir)
}

createFileOnPath(dir) {
  global editor
  result := InputBox("", "New file name", "w300 h100")
  if (result.Result != "OK")
    return
  path := dir "\" result.Value
  FileAppend("", path)
  Run(editor ' "' path '"')
}

createDir() {
  PostMessage(0x111, 41504, 0)  ; refresh
  SendInput("{AppsKey}wf")
}

openSelectionInViewer() {
  global viewer
  selection := getSelection()
  Run(viewer ' "' selection '"')
}

#+q::WinSetAlwaysOnTop(-1, "A")

+<^`;:: {
  showHideAppWin("ahk_exe alacritty.exe", "C:\Program Files\Alacritty\alacritty.exe --config-file C:\home\etc\dotfiles\alacritty\alacritty.toml")
}

; ------------------------------------------------------------------------
; Functions
; ------------------------------------------------------------------------

getSelection() {
  theClipboard := ClipboardAll()
  A_Clipboard := ""
  Send("^c")
  if !ClipWait(1) {
    A_Clipboard := theClipboard
    return ""
  }
  selection := A_Clipboard
  A_Clipboard := theClipboard
  return selection
}

showHideAppWin(winTitle, appPath) {
  if WinExist(winTitle) {
    if WinActive() {
      Send("!{Tab}")
    } else {
      WinActivate()
    }
  } else {
    Run(appPath)
  }
}

showAppWin(winTitle, appPath) {
  if WinExist(winTitle) {
    WinActivate()
  } else {
    Run(appPath)
  }
}

;---
; Vim layer
;---

Escape::Send("{Blind}{Escape}")

Escape & h::Send("{Blind}{Left}")
Escape & j::Send("{Blind}{Down}")
Escape & k::Send("{Blind}{Up}")
Escape & l::Send("{Blind}{Right}")

Escape & w::Send("{Blind}^{Right}")
Escape & b::Send("{Blind}^{Left}")

Escape & 4::Send("{Blind}{End}")
Escape & 0::Send("{Blind}{Home}")

Escape & `;::Send("{Blind}{Backspace}")
Escape & p::Send("{Blind}{Del}")

Escape & '::Send("{Blind}^{Backspace}")

Escape & m::AltTab
;Escape & n::WinMinimize("A")
Escape & .::Send("{Blind}^s")

Escape & Space::Send("{Blind}!{Space}")

Escape & [::Send("{Blind}^#{Left}")
Escape & ]::Send("{Blind}^#{Right}")
